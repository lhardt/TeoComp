// MÁQUINA NORMA.
// --------------------------------
// Trabalho de Teoria da Computação
// --------------------------------
// Léo Hardt e Laura Ramos
// 13/12/2022

// Q2.a. Crie uma função de codificação pares como números naturais,
// da seguinte forma:  cod(a,b) = 2^a  * (2b + 1)
//
// Essa função deve preservar A e B.

// A := 0
operation clear(A)
{
 1: if zero A then goto 0 else goto 2
 2: do dec A goto 1
}

// A := A+B,  B := 0
operation soma2(A,B)
{
    1: if zero B then goto 0 else goto 2
    2: do dec B goto 3
    3: do inc A goto 1
}

operation load2(A,B)
{
    1: do clear(A) goto 2
    2: do soma2(A,B) goto 0
}


// A := A + B,  B constante, C deve estar zerado
operation soma3(A,B,C)
{
  1: if zero B then goto 5 else goto 2
  2: do dec B goto 3
  3: do inc A goto 4
  4: do inc C goto 1
  5: if zero C then goto 0 else goto 6
  6: do dec C goto 7
  7: do inc B goto 5
}

// A := B,  B constante, C deve estar zerado
operation load3(A,B,C)
{
  1: do clear(A) goto 2
  2: do soma3(A,B,C) goto 0
}

// B := 2^A, usando I,J
operation exp(A,B)
{
  1: do inc B goto 2
  2: do inc B goto 3
  3: do load3(I,B,Z) goto 4
  4: do dec A goto 5
  5: if zero A then goto 0 else goto 6
  6: do soma3(B,I,J) goto 7
  7: do load3(I,B,J) goto 8
  8: do dec A goto 9
  9: if zero A then goto 0 else goto 6
}

// A = B*C, usando K
operation mult(A,B,C)
{
  1: do load3(A,C,K) goto 2
  2: do dec B goto 3
  3: do soma3(A,C,K) goto 4
  4: do dec B goto 5
  5: if zero B then goto 0 else goto 3
}

// Codifica um par A,B como a saida Y.
operation cod(Y, A, B)
{
  // cod(a,b) = 2^a  * (2b + 1)
  // D, E := B    F := A
  1: do load3(D,B,E) goto 2
  2: do load3(E,B,F) goto 3
  3: do load3(F,A,G) goto 4

  // D := 2b + 1
  4: do soma2(D,E) goto 5
  5: do inc D goto 6

  // L := 2^a
  6: if zero A then goto 7 else goto 8
  7: do load3(Y,D,G) goto 0
  8: do exp(F,L) goto 9

  // Y := 2^a  * (2b + 1)  = D * L
  9: do mult(Y,D,L) goto 0
}

main
{
   // Fariamos operacoes com a entrada,
   // e depois teriamos que codificar a,b.

    // A = 2, B = 3 serao codificados
    1: do inc A goto 2
    2: do inc A goto 3
    3: do inc B goto 4
    4: do inc B goto 5
    5: do inc B goto 6

    6: do cod(Y, A,B) goto 0
}