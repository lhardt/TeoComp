// MÁQUINA NORMA.
// --------------------------------
// Trabalho de Teoria da Computação
// --------------------------------
// Léo Hardt e Laura Ramos
// 13/12/2022

// Q4.a Considere a sequência de Fibonacci, com F0=0, F1=1, ...,
// F11 = 89. Construa um programa para a máquina Norma que tenha
// o valor de f(x) = F(x) como função computada.

// A := 0
operation clear(A)
{
 1: if zero A then goto 0 else goto 2
 2: do dec A goto 1
}

// A := A + B,  B constante, C deve estar zerado
operation soma3(A,B,C)
{
  1: if zero B then goto 5 else goto 2
  2: do dec B goto 3
  3: do inc A goto 4
  4: do inc C goto 1
  5: if zero C then goto 0 else goto 6
  6: do dec C goto 7
  7: do inc B goto 5
}

// A := B,  B constante, C deve estar zerado
operation load3(A,B,C)
{
  1: do clear(A) goto 2
  2: do soma3(A,B,C) goto 0
}

// A := A+B,  B := 0
operation soma2(A,B)
{
    1: if zero B then goto 0 else goto 2
    2: do dec B goto 3
    3: do inc A goto 1
}

// A := B,  B := 0
operation load2(A,B)
{
    1: do clear(A) goto 2
    2: do soma2(A,B) goto 0
}

// A += B, B := A antigo
operation nextfib(A,B, aux1, aux2) {
    // aux1 := A. O 'A antigo'
    1: do load3(aux1, A, aux2) goto 2
    2: do soma3(A, B, aux2) goto 3
    3: do load3(B,aux1,aux2) goto 0
}

// Da o n-esimo valor de fibonacci, em A.
operation fibonacci(A, B, aux1, aux2, count){
    // Uma base para nossa soma. A, o valor final, ainda eh 0
    1: do inc B goto 2

    // Loop: executa nextfib 'aux' vezes.
    2: if zero count then goto 0 else goto 3
    3: do nextfib(A, B, aux1, aux2) goto 4
    4: do dec count goto 2
}

main {
    1: do fibonacci(A,B, K, L, X)  goto 2
    2: do load2(Y, A) goto 0
}